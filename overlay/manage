#! /usr/bin/perl -w


use FindBin;
use File::Spec;
use File::Temp;
use ShellCmd_1A7E54C0_9EFF_11D9_BFF3_00A0C9EF1631;


use constant pristine_tree => "vendor";
use constant patched_tree => "patched";
use constant more_toplevel_excludes => qw/README usr/;
use constant DEBUGGING => 0;


our($ROOT, $cfg_archive, $cfg_RCS, $tmpname, $sh);


{
   package directory;
   use Carp;
   
   
   sub name {
      return shift->{name};
   }


   sub fail {
      my($me, $msg)= @_;
      croak qq'$msg directory "' . $me->name . qq'": $!';
   }   

   
   sub new {
      my(undef, $name)= @_;
      my $me= bless {};
      local *DIR;
      opendir DIR, $me->{name}= $name or $me->fail("Could not open");
      $me->{dh}= *DIR{IO};
      return $me;
   }
   
   
   DESTROY {
      my $me= shift;
      if (my $dh= $me->{dh}) {
         closedir $dh or $me->fail("Could not close");
      }
   }
   
   
   sub read {
      die if wantarray;
      return readdir shift->{dh};
   }
}


{
   package file;
   use Carp;
   
   
   sub name {
      return shift->{name};
   }


   sub fail {
      my($me, $msg)= @_;
      croak qq'$msg file "' . $me->name . qq'": $!';
   }   

   
   sub new {
      my(undef, $mode, $name)= @_;
      my $me= bless {};
      local *FILE;
      open FILE, $mode, $me->{name}= $name or $me->fail("Could not open");
      $me->{fh}= *FILE{IO};
      return $me;
   }
   
   
   DESTROY {
      my $me= shift;
      if (my $fh= $me->{fh}) {
         close $fh or $me->fail("Could not close");
      }
   }
   
   
   sub readline {
      my $me= shift;
      my $line;
      die if wantarray;
      undef $!;
      unless (defined($line= readline $me->{fh})) {
         $me->fail("Error while reading from") if $!;
      }
      return $line;
   }
}


sub ensure_dir($) {
   my($rpath)= @_;
   -d $rpath && return;
   mkdir $rpath or die qq'Could not create directory "$rpath": $!';
   -d $rpath or die $!;
}


# Returns the file modification time from status in variable '_'.
sub mtime() {
   return +(stat _)[9];
}


sub updating($;$) {
   my($to, $from)= @_;
   $from||= "(without contents)";
   print STDERR qq'Updating file "$to" from file "$from".\n';
}


sub reconcile_file {
   my($vendor, $ovl, $pchd, $live, $psd)= @_;
   my($arch, $t, $x);
   if (defined $cfg_archive) {
      my($vol, $dirs1, $dirs2);
      ($vol, $dirs1)= File::Spec->splitpath($cfg_archive, 1);
      (undef, $dirs2, $arch)= File::Spec->splitpath($live);
      $arch= File::Spec->catpath(
         $vol, File::Spec->catdir($dirs1, $dirs2), $arch
      );
      $arch.= ',v' if $cfg_RCS;
   }
   undef $psd if -l $psd || ! -d _;
   # Update vendor file from archive.
   if (
         defined($arch) && -f $arch && ($t= mtime , 1)
      && (!-f $vendor || mtime < $t)
   ) {
      if ($cfg_RCS) {
         $sh->try_next;
         if ($sh->call(qq{rlog "$arch" | grep -q '^branches:'}) == 0) {
            # Vendor branch is present.
            updating($vendor, $arch);
            $sh->call(qq'co -p -r1.1.1 "$arch" > "$tmpname" 2> /dev/null');
         } else {
            # No vendor branch, create an empty file.
            # This avoids looking up the vendor branch over and over again
            # in later runs.
            updating($vendor);
            $sh->call(qq': > "$tmpname"');
         }
         $sh->call('touch', '--reference=' . $arch, '--', $tmpname);
         $sh->call(qw"mv -f --", $tmpname, $vendor);
      } else {
         updating($vendor, $arch);
         $sh->call(qw"cp -af --", $arch, $vendor);
      }
   }
   # Reconcile overlay template file and vendor file.
   # This is only done if the overlay file is actually a template.
   if (defined $psd) {
      -f $ovl or die qq'File "$ovl" has to exist';
      $t= mtime;
      if (($x= -f $vendor) && mtime > $t) {
         # Vendor file exists and is newer. Update overlay template.
         if (-s $vendor) {
            # Vendor file is not empty.
            updating($ovl, $vendor);
            $sh->call(qw'cp -af --', $vendor, $ovl);
         } else {
            warn
                 qq'Empty vendor file "$vendor" '
               . qq'is newer than overlay file "$ovl"'
            ;
         }
      } elsif (!$x || $t > mtime) {
         # Overlay template is newer or vendor file does not exist.
         # Update vendor file.
         updating($vendor, $ovl);
         $sh->call(qw'cp -af --', $ovl, $vendor);
      }
   }
}


sub manage {
   my($basedir, $rpath, $exclusions)= @_;
   my(@d, $entry, $path, $abspath);
   $abspath= File::Spec->catdir($basedir, $rpath);
   {
      my $dh= new directory($abspath);
      while (defined($entry= $dh->read)) {
         next if exists $exclusions->{$entry};
         next unless File::Spec->no_upwards($entry);
         $path= File::Spec->catfile($abspath, $entry);
         if (-l $path || -f _) {
            reconcile_file
                  +(
                     map {
                          defined()
                        ? File::Spec->catfile(
                             File::Spec->catdir($basedir, $_, $rpath), $entry
                          )
                        : $path
                        ;
                     } pristine_tree, undef, patched_tree
                  )
               , File::Spec->catfile(
                    File::Spec->catdir($ROOT, $rpath), $entry
                 )
               , "$path.patches"
            ;
         } elsif (-d _) {
            push @d, $entry unless $entry =~ /.+\.patches$/;
         }
      }
   }
   foreach $entry (@d) {
      $path= File::Spec->catdir($rpath, $entry);
      foreach (patched_tree, pristine_tree) {
         ensure_dir +File::Spec->catdir($basedir, $_, $path);
      }
      &manage($basedir, $path, $exclusions);
   }
}


$tmpname= File::Spec->catfile(File::Temp::tempdir(CLEANUP => 1), "tmp");
$sh= new Lib::ShellCmd(
   -log => DEBUGGING ? *STDOUT{IO} : undef, -noop => !!DEBUGGING
);
# Honor the ROOT variable from "/etc/make.conf".
$ROOT= qx{emerge --info | grep '^ROOT='};
$ROOT= qx{eval "$ROOT" && printf '%s' \$ROOT} || File::Spec->rootdir;
{
   # Determine config file path.
   use constant cfg_path => '/etc/dispatch-conf.conf';
   my($vol, $subdirs, $file)= File::Spec->splitpath(cfg_path);
   my $cfg= File::Spec->catfile(
        File::Spec->catdir(File::Spec->catpath($vol, $ROOT, ''), $subdirs)
      , $file
   );
   if (-f $cfg) {
      # Read config file.
      my $cfg= new file('<', $cfg);
      while (defined($_= $cfg->readline)) {
         $cfg_RCS= 1 if /^use-rcs\s*=\s*yes\s*$/i;
         if (/^archive-dir\s*=\s*(.+?)\s*$/i) {
            (undef, $cfg_archive)= File::Spec->splitpath($1, 1);
            my($vol, $root)= File::Spec->splitpath($ROOT, 1);
            $cfg_archive= File::Spec->catpath(
               $vol, File::Spec->catdir($root, $cfg_archive), ''
            );
         }
      }
   }
}
my $basedir= $FindBin::Bin;
foreach (pristine_tree, patched_tree) {
   ensure_dir +File::Spec->catdir($basedir, $_);
}
&manage(
     $basedir, File::Spec->curdir
     , {
        map {$_, undef}
          $FindBin::Script, more_toplevel_excludes
        , pristine_tree, patched_tree
     }
);
